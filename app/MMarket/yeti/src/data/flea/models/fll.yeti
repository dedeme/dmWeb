// Copyright 21-Apr-2020 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Follow investor model.
module data.flea.models.fll;

matrix = load data.matrix;
fmodel = load data.flea.fmodel;

(
  fcalc closes params action
  is ~double[][] ->
    array<number> ->
    (~double[] -> ~double[] -> ()) ->
    () =
  ( nDays = length closes;
    nCos = length closes[0];
    percToBuy = params[0];
    percToSell = params[1];

    refs = matrix.mk nCos;
    toBuys = matrix.mkBoolean nCos;
    for [0..nCos - 1] do iCo:
      iDay = avoid do iDay:
                     closes[iDay][iCo] <= 0
                     done
                   [0..nDays - 1];
      refs[iCo] := closes[iDay][iCo] * (1 - percToSell);
      toBuys[iCo] := false;
      done;

    for [0..nDays - 1] do iDay:
      for [0..nCos - 1] do iCo:
        q = closes[iDay][iCo];
        ref = refs[iCo];
        if q > 0 then
          if toBuys[iCo] then
            if q > ref then
              refs[iCo] := q * (1 - percToSell);
              toBuys[iCo] := false
            else
              newRef = q * (1 + percToBuy);
              if newRef < ref then
                refs[iCo] := newRef;
              fi
            fi
          else
            if q < ref then
              refs[iCo] := q * (1 + percToBuy);
              toBuys[iCo] := true
            else
              newRef = q * (1 - percToSell);
              if newRef > ref then
                refs[iCo] := newRef
              fi
            fi
          fi
        fi
        done;
      action closes[iDay] refs;
      done;
  );
{
  /// Creates a model.
  mk ()
  is () -> fmodel.t =
    { id = "FLL",
      name = "Follow",
      parNames = ["% C", "% V"],
      parMins = array [0.05, 0.05],
      parMaxs = array [0.3, 0.3],
      parDecs = array [6, 6],
      fcalc
    },
})
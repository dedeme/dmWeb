// Copyright 10-May-2020 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

// Servers configuration page.
module server.pgs.settings.serversConfiguration;

cgi = load es.dm.cgi;
json = load es.dm.json;
load es.dm.jsonM;
sync = load es.dm.sync;
load std;
net = load net.net;
log = load db.log;
nicksTb = load db.nicksTb;

{
  /// Request process.
  ///
  /// Arguments:
  ///   ck - Comunication key.
  ///   rq - Request.
  process ck rq
  is string -> hash<string, json.t> -> string =
    case cgi.rqString rq "rq" of
    "dailyTest" :
      serverId = cgi.rqNumber rq "serverId";
      rp = [:];
      sync.run do lk:
        try
          rp["ok"] := json.wb (net.testDailyConf lk serverId)
        catch Exception ex:
          log.exc lk ex;
          rp["ok"] := json.wb false
        yrt
        done;
      cgi.rp ck rp;
    "historicTest" :
      serverId = cgi.rqNumber rq "serverId";
      nickId = cgi.rqNumber rq "nickId";
      rp = [:];
      sync.run do lk:
        case nicksTb.getNick lk nickId of
        Some nick:
          try
            rp["ok"] := json.wb (net.testHistoricConf lk serverId nick)
          catch Exception ex:
            log.exc lk ex;
            rp["ok"] := json.wb false
          yrt;
        None ():
          log.error lk "Nick id \(nickId) not found";
          rp["ok"] := json.wb false
        esac
        done;
      cgi.rp ck rp;
    k :
      failedKey k
    esac,
}
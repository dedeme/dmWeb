// Copyright 11-Jun-2020 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Accounting-balance page.
module server.pgs.acc.balance;

load es.dm.std;
cgi = load es.dm.cgi;
json = load es.dm.json;
load es.dm.jsonM;
sync = load es.dm.sync;
acc = load data.acc;
cts = load data.cts;
qtable = load data.qtable;
fmodel = load data.flea.fmodel;
manager = load data.manager;
diariesDb = load db.acc.diariesDb;
nicksTb = load db.nicksTb;
dailyTb = load db.dailyTb;
quotesDb = load db.quotesDb;
managersTb = load db.managersTb;
conf = load db.conf;

load std;

{
  /// Request process.
  ///
  /// Arguments:
  ///   ck - Comunication key.
  ///   rq - Request.
  process ck rq
  is string -> hash<string, json.t> -> string =
    case cgi.rqString rq "rq" of
    "idata" :
      var rp = [:];
      sync.run do lk:
        {activity} = conf.activity ();
        closes = quotesDb.closes lk;
        qs = dailyTb.read lk;
        mqs = [:];
        for qs do q:
          case nicksTb.getNick lk q.nick of
          Some nk : mqs[nk.name] := q.value;
          None () : ()
          esac
          done;
        mans = managersTb.read lk;
        var ledgers = [];
        var portfolios = [];
        for [0..cts.managers - 1] do i:
          anns = diariesDb.readAnnotations lk i;
          {ledger, portfolio} = acc.settlement anns;
          ledgers := acc.ledgerToJs ledger::ledgers;
          portfolio = map do e:
            nick = e.nick;
            var quote = -1;
            var ref = e.price;
            ocs = if activity != cts.actSleeping2
                  then
                    if nick in mqs then
                      qtable.nickValuesAdd closes nick mqs[nick]
                    else
                      qtable.nickValues closes nick
                    fi
                  else
                    qtable.nickValues closes nick
                  fi;
            {model, params} = manager.getModel mans[i] nick;
            case ocs of
            Some cs :
              quote := (cs is ~double[][])[length cs - 1][0];
              refs = fmodel.refs model cs params;
              ref := head (reverse refs);
              if ref > quote then ref := quote fi; // Sell situation.
            None () :
              ()
            esac;
            { nick, stocks = e.stocks, price = e.price, quote, ref }
            done portfolio;
          portfolios := jswList acc.pfEntryToJs portfolio::portfolios
          done;
        rp["ledgers"] := reverse ledgers |> json.wa;
        rp["portfolios"] := reverse portfolios |> json.wa;
        done;
      cgi.rp ck rp;
    k:
      failedKey k
    esac,
}

// Copyright 06-Mar-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Index tree data.

/// \s, [s], [m...] -> m
new = \id, doc, subtrees ->
  return {
    "id": id,
    "doc": doc,
    "subtrees": subtrees
  };
;

/// \m -> s
toJs = \IxT ->{
 return js.wa([
    js.ws(IxT.id),
    arr.size(IxT.doc) == 0 ? js.wn() : js.ws(IxT.doc[0]),
    js.wa(arr.mp(IxT.subtrees, \T -> return toJs(T);))
  ]);
};

/// \s -> m
fromJs = \s -> {
  a = js.ra(s);
  return new(
    js.rs(a[0]),
    js.isNull(a[1]) ? [] : [js.rs(a[1])],
    arr.mp(js.ra(a[2]), \ts -> return fromJs(ts);)
  );
};

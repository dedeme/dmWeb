// Copyright 06-Mar-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Package index reader.

import "db/pathsTb";
import "data/ixTree";

// Read module documentation.
// \s -> s
docRead = \fpath -> {
  if (!file.exists(fpath)) return "";

  R = [""];
  f = file.ropen(fpath);
  while () {
    L = file.readLine(f);
    if (arr.size(L) == 0) break;

    l = str.trim(L!);
    if (str.starts(l, "import ")) break;
    if (str.starts(l, "///")) {
      R! = l[4:];
      break;
    }
  }
  file.close(f);
  return R!;
};

// Adds trees to node
// \[m], s -> ()
completeTrees = \Trees, dir -> {
  Fnames = file.dir(dir);
  for (fname : Fnames) {
    fpath = path.cat([dir, fname]);
    if (file.isDirectory(fpath)) {
      NewTree = ixTree.new(fname, [], []);
      arr.push(Trees, NewTree);
      completeTrees(NewTree.subtrees, fpath);
    } else {
      if (!str.ends(fname, ".kut")) continue;
      arr.push(Trees, ixTree.new(fname[:-4], [docRead(fpath)], []));
    }
  }
};

/// Reads package index tree.
/// \s -> [m]
read = \pack -> {
  Ppath = arr.find(pathsTb.read(), \P -> return P.id == pack;);
  if (arr.size(Ppath) == 0) return [];

  root = Ppath!.path;
  if (!file.exists(root)) return [];

  R = ixTree.new("", [], []);
  completeTrees(R.subtrees, root);
  return [R];
};

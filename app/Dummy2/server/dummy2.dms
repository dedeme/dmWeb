// Copyright 25-Sep-2019 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

"dm/cgi" import
"conf" import
"pages/main" import
"pages/settings" import
"pages/chpass" import
"pages/backups" import

"Dummy2" appName =
"201909" dataVersion =
"dmcgi/Dummy2" appDir =
3600 expiration =
appName : "\nData version: " : dataVersion : "\n" + + + version =

( // () -> Void
  (fname = cgi,home fname path,+ file,mkdir)
  mkdir =>

  cgi,home "data" path,+
  dir =
  dir file,exists? !
  (
    dir file,mkdir

    dir "version.txt" path,+ version file,write

    "tmp" mkdir
    "trash" mkdir
    "backups" mkdir
  )
  if
) appInit =>

( Rq =; sessionId =
  ("Field 'page' is missing" fail) (Rq "page" obj,has? !) if
  Rq "page" obj,get js,rs : page =

  ( page "Main" ==)
  ( Rq main,process)
  else
  ( page "logout" ==)
  (
    sessionId cgi,delSession : rt =  // Dummy return
    Rq "rq" "logout" js,ws obj,put
    Rq "" "" backups, process  // Add automatic backup
    rt
  )
  else
  (page "Settings" ==)
  (Rq settings,process)
  else
  (page "Chpass" ==)
  (Rq chpass,process)
  else
  (page "Backups" ==)
  (Rq appName version backups,process)
  else
  ( "Value '" page "' not allowed for 'page'" + + fail)
  if
) process =>

// Start ---------------------------------------------------------------------

appDir expiration cgi,init
appInit
conf,init

sys,args 0 get rq =
rq ":" str,index ix =

( ix -1 ==) //...................................................... CONNECTION
(
  rq cgi,setKey
  rq cgi,connect sys,print
)
else
(ix 0 ==) //................................................... AUTHENTICATION
(
  appName cgi,klen cryp,key : key =
  key cgi,setKey
  rq 1 str,right key cryp,decryp : ":" : str,split : Fields =

  Fields 0 get : Fields 1 get : Fields 2 get "1" == : cgi,authentication
    sys,print
)
else //........................................................... NORMAL DATA
(
  rq ix str,left : sessionId =
  rq ix 1 + str,right : crypData =

  ( "nosession" cgi,setKey; cgi,expired sys,print)
  ( Ss =
    Ss "key" obj,get js,rs : key =
    Ss "conId" obj,get js,rs : connectionId =

    key cgi,setKey
    crypData key cryp,decryp : js,ro : Rq =

    ( 0)
    else
    ( (1) else (0) (Rq "connectionId" obj,get : connectionId : ==) if)
    (Rq "connectionId" obj,has?)
    if

    ( sessionId Rq process sys,print)
    else
    ( "nosession" cgi,setKey; cgi,expired sys,print)
    ()
    if
  )
  (sessionId cgi,getSession)
  wrap,option
)
if



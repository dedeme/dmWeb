// Extract quotes from Infobolsa.
//
// Process:
//    1. Read infoolsa data. For example:
//      https://www.infobolsa.es/cotizacion/historico-baviera?
//        startDate=20220630&endDate=20221118
//    2. Copy page source in a file placed in the same directory that
//       'extractQuotesInfobolsa.kut' with name 'qs0.txt'.
//    3. Launch 'extractQuotesInfobolsa.kut' from its directory.
// A file called 'qs1.txt' with data in format KtMarket will be created.
//
// - Care about number of quotes (must be 610)

fmtDate = \d -> return d[6:] + d[3:5] + d[:2];;

fmtN = \n -> return str.replace(str.replace(n, ".", ""), ",", ".");;

fmax = \n1, n2 ->
  return math.toFloat(fmtN(n1)) > math.toFloat(fmtN(n2)) ? n1 : n2;;

fmin = \n1, n2 ->
  return math.toFloat(fmtN(n1)) < math.toFloat(fmtN(n2)) ? n1 : n2;;

Html = [file.read("qs0.txt")];
Ix = [str.index(Html!, "<tbody>")];
Html! = Html![Ix!:];
Ix! = str.index(Html!, "</tbody>");
Html! = Html![:Ix!];

Ix! = str.index(Html!, "date center");
Data = [""];
N = [0];
while (Ix! != -1) {
  Html! = Html![Ix! + 13:];
  Ix! = str.index(Html!, "<");
  date = str.trim(Html![:Ix!]);

  Ix! = str.index(Html!, "price");
  Html! = Html![Ix! + 7:];
  Ix! = str.index(Html!, "<");
  close = str.trim(Html![:Ix!]);

  Ix! = str.index(Html!, "cotiz");
  Html! = Html![Ix! + 7:];
  Ix! = str.index(Html!, "<");
  open = str.trim(Html![:Ix!]);

  Ix! = str.index(Html!, "max");
  Html! = Html![Ix! + 5:];
  Ix! = str.index(Html!, "<");
  max0 = str.trim(Html![:Ix!]);

  Ix! = str.index(Html!, "min");
  Html! = Html![Ix! + 5:];
  Ix! = str.index(Html!, "<");
  min0 = str.trim(Html![:Ix!]);

  max = fmax(fmax(fmax(max0, open), close), min0);

  min = fmin(fmin(fmin(min0, open), close), max);

  Ix! = str.index(Html!, "volume");
  Html! = Html![Ix! + 8:];
  Ix! = str.index(Html!, "<");
  vol = str.trim(Html![:Ix!]);

  line = fmtDate(date) +
   ":" + fmtN(open) +
   ":" + fmtN(close) +
   ":" + fmtN(max) +
   ":" + fmtN(min) +
   ":" + fmtN(vol) +
   ":false\n"
  ;

  Data! += line;
  N! += 1;

  Ix! = str.index(Html!, "date center");
}

file.write("qs1.txt", Data!);

sys.println(math.toStr(N!));



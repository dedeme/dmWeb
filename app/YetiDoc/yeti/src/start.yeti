module start;

import java.lang.Throwable;

load const;
cgi = load es.dm.cgi;

load es.dm.std;
{exists?, mkdir} = load es.dm.file;
{error, print} = load es.dm.exc;
{key, decryp} = load es.dm.cryp;

db = load data.db;
menuPath = load data.menuPath;
paths = load pages.paths;
pindex = load pages.pindex;
mod = load pages.mod;
code = load pages.code;
chpass = load core.changePass;

dataDir = appDir ^/ "data";

/* functions ------------------------------------------------------ */

init () =
  if exists? dataDir then
    // db.create (cgi.home ()) versionText;
    db.init (cgi.home ());
  else
    mkdir dataDir;
    db.create (cgi.home ()) (versionText ())
  fi;

process sessionId rq =
  (load es.dm.json;
   case rstring rq["rq"] of
   "idata":
      a = rarray (db.getMainData () |> jsOfStr);
      cgi.ok [
        "menu": a[0],
        "lang": a[1],
        "showAll": a[2],
        "paths": db.getPaths () |> wlist menuPath.toJson
      ];
    "bye": cgi.delSession sessionId;
    s: throw (error "Request '\(s)' is unknown in main.rq")
  esac);

hub sessionId rq =
  (load es.dm.json;
   case rstring rq["page"] of
   "main": process sessionId rq;
   "paths": paths.process rq;
   "index": pindex.process rq;
   "module": mod.process rq;
   "code": code.process rq;
   "changePass": chpass.process rq;
   s: throw (error "Page '\(s)' is unknown"); cgi.okEmpty();
   esac);

/* main ----------------------------------------------------------- */

main args =
  try
    cgi.cgiInit appName appDir appExpiration "Yeti doc interesting key";
    init ();
    load es.dm.std;
    case args of
    rq::_: (
      case cindex ":" rq of
      None (): // ................................................. CONNECTION
        cgi.setKey rq;
        cgi.send (cgi.connect rq);
      Some 0: // ...............................................AUTHENTICATION
        key = key (cgi.klen ()) (cgi.appName ());
        cgi.setKey key;
        a = array (strRight rq 1 |> decryp key |> csplit ':');
        rp = cgi.authenticate a[0] a[1] (a[2] == "1");
        cgi.send rp;
     Some i: // ...................................................NORMAL DATA
        sessionId = strLeft rq i;
        data = strRight rq (i + 1);
        case cgi.getSessionData sessionId of
        None (): cgi.send (cgi.expired ());
        Some {comKey, conId}:
          load es.dm.json;
          cgi.setKey comKey;
          obj = decryp comKey data |> jsOfStr |> robject;
          if "connectionId" in obj
          then
            if rstring obj["connectionId"] == conId
            then cgi.send (hub sessionId obj)
            else cgi.send (cgi.expired ())
            fi
          else
            cgi.send (hub sessionId obj)
          fi
        esac
      esac);
    a: throw (error "Sys.argv has \(length a) parameters and must have 1")
    esac;
  catch Throwable ex:
    print ex;
  yrt;

class Main
  static void main(String[] args)
    main (args)
end;
